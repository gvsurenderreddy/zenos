# $Id: PKGBUILD 22882 2008-12-29 19:41:01Z tpowa $
# Contributor: Paul Mattal <paul@mattal.com>
# Maintainer: Paul Mattal <pjmattal@elys.com>
pkgname=aufs-utils
pkgver=20081226
pkgrel=3
_kernver='2.6.28-ARCH'
pkgdesc="Another Unionfs Implementation that supports NFS branches"
arch=('i686' 'x86_64')
url="http://aufs.sourceforge.net/"
license=('GPL2')
depends=('glibc')
source=(ftp://ftp.archlinux.org/other/aufs/aufs-$pkgver.tar.bz2)

build() {
  cd $srcdir/aufs || return 1

  # Fix TMPFS_MAGIC error
  sed 's|-le 26|-le 27|g' -i fs/aufs25/Makefile || return 1

  # disable debug, broken in 2.6.28!
  sed -i 's|CONFIG_AUFS_DEBUG = y|CONFIG_AUFS_DEBUG = |' \
     local.mk || return 1

  # configure for NFS by:
  # 1) configure local.mk to use FILP and LHASH
  sed -i 's|CONFIG_AUFS_PUT_FILP_PATCH =|CONFIG_AUFS_PUT_FILP_PATCH = y|' \
    local.mk || return 1
  sed -i 's|CONFIG_AUFS_LHASH_PATCH =|CONFIG_AUFS_LHASH_PATCH = y|' \
    local.mk || return 1
  # 2) configure local.mk NOT to use FAKE_DM
  sed -i 's|CONFIG_AUFS_FAKE_DM = y|CONFIG_AUFS_FAKE_DM =  |' \
    local.mk || return 1

  # build
  make KDIR=/usr/src/linux-$_kernver/ -f local.mk || return 1

  # install
  install -D -m755 util/aufind.sh $pkgdir/usr/bin/aufind.sh || return 1
  install -D -m755 util/aulchown $pkgdir/usr/bin/aulchown || return 1
  install -D -m755 util/auplink $pkgdir/usr/bin/auplink || return 1
  install -D -m755 util/mount.aufs $pkgdir/sbin/mount.aufs || return 1
  install -D -m755 util/umount.aufs $pkgdir/sbin/umount.aufs || return 1
  install -D -m644 util/etc_default_aufs $pkgdir/etc/default/aufs || return 1
}

# vim:set ts=2 sw=2 et:
md5sums=('e855ed9edc16953028f22cae393690de')
