# Contributor: tuxce <tuxce.net@gmail.com>

pkgname=pacman-static
_pkgname=pacman
pkgver=3.2.1
pkgrel=1
pkgdesc="Static version of pacman executable"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('pacman>=3.2.1-1')
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/pacman/$_pkgname-$pkgver.tar.gz)
md5sums=('5b133613cebd74aa8c199de049e25cc4')

build() {
  cd $startdir/src/$_pkgname-$pkgver
  echo 'SUBDIRS = src/pacman' > Makefile.am
  sed -i -e 's/bin_PROGRAMS = pacman/bin_PROGRAMS = pacman.static/' \
    -e 's|$(top_builddir)/lib/libalpm/.libs/libalpm.la|-lalpm|' \
    src/pacman/Makefile.am
  echo -e '\npacman_static_SOURCES = $(pacman_SOURCES)\npacman_static_LDFLAGS = $(LDFLAGS) -all-static' >> src/pacman/Makefile.am

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

}

# vim: set ts=2 sw=2 et:
